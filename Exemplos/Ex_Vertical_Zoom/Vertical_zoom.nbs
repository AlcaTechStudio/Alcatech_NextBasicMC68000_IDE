import"\system\genesis_header.asm" ' Header de uma ROM de mega Drive Padrão (deve ficar sempre no topo)


std_init()

load_tiles_dma(addressof(tiles),210,1)
load_cram_dma(addressof(paleta),16,0)

for y = 0 to 14
 for x = 0 to 15
 draw_tile(x + (y*15) + 1, x+12,y+2, Plane_A)
 next 
next  
'[...] O que for colocado aqui sera executado apenas uma vez

camZ = 0 ' Variavel que controla o Scaling

' Interrupção Vertical (ativado por default)
Enable_H_int()  ' Ativa Interrupção horizontal
set_hint_counter(0) ' Interrupção vai ocorrer em todas as scanlines
enable_global_int() ' Ativa interrupções Globais

Do 'main
j = joypad6b_read(0) ' Lê o Joystick 1

' Incrementa o decrementa o valor da camera de acordo com o input do joystick
if bit_test(j, btn_up) then
 camZ+=1
 elseif bit_test(j,btn_down) then
 camZ-=1
end if
' Limita o valor do Scalling
if  _signed(camZ <= - 194) then
 camZ = -194
elseif _signed(camZ>= 576) then 'É necessario usar _signed nesse caso, pois nossa variavel pode assumir valores negativos
 camZ = 576
end if

'Espera o Proximo Frame
vb_flag = 1
while(vb_flag)_asm("nop")

Loop ' Laço infinito

sub isr_Vblank() ' Interrupção Vertical
vb_flag = 0 ' Limpa o Flag de interrupção Vertical (marca o inicio do proximo frame)
end sub

sub isr_Hblank() ' Interrupção Horizontal (scaling no Plano A)
push(&hC00004   as long,  "A1" )
push(&h40000010 as long, "(A1)")
push((pop( "4(A1)" as byte) * camZ)>>7 as word, "-4(A1)")
end sub



import"\system\genesis_std.nbs" ' Biblioteca contendo funções standard do Mega Drive

paleta:
	DATAINT	&h0000,&h0000,&h0644,&h0244,&h028C,&h0A66,&h0C88,&h0004
	DATAINT	&h0CEE,&h0008,&h000C,&h0268,&h0246,&h0000,&h0000,&h0000
tiles:
import"\tiles.bin"	